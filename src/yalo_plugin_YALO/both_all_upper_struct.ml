(**************************************************************************)
(*                                                                        *)
(*  Copyright (c) 2025 OCamlPro SAS                                       *)
(*                                                                        *)
(*  All rights reserved.                                                  *)
(*  This file is distributed under the terms of the GNU Lesser General    *)
(*  Public License version 2.1, with the special exception on linking     *)
(*  described in the LICENSE.md file in the root directory.               *)
(*                                                                        *)
(*                                                                        *)
(**************************************************************************)

open EzCompat (* for StringSet *)
open Yalo.V1
open Yalo_plugin_ocaml.V1

let lint_msg = "Inner modules should be full uppercase"

open OCAML_TAST

[%%if ocaml_version < (4, 10, 0)]
let module_expr check ~file ~linter me =
  match me.mod_desc with
  | Tmod_functor (_id, name_op_loc, Some mty, _) ->
      check ~file ~linter name_op_loc mty.mty_type
  | _ -> ()

let module_type check ~file ~linter mty =
  match mty.mty_desc with
  | Tmty_functor (_id, name_op_loc, Some mty, _) ->
      check ~file ~linter name_op_loc mty.mty_type
  | _ -> ()
[%%else]
let module_expr check ~file ~linter me =
  match me.mod_desc with
  | Tmod_functor (Named (_id, name_op_loc, mty), _) ->
      check ~file ~linter name_op_loc mty.mty_type
  | _ -> ()

let module_type check ~file ~linter mty =
  match mty.mty_desc with
  | Tmty_functor (Named (_id, name_op_loc, mty), _) ->
      check ~file ~linter name_op_loc mty.mty_type
  | _ -> ()
[%%endif]

let uppercase s =
  let len = String.length s in
  let b = Buffer.create (2*len) in
  let rec iter need_underscore i =
    if i < len then
      let c = s.[i] in
      match c with
      | 'a'..'z' ->
          Buffer.add_char b (Char.uppercase_ascii c);
          iter true (i+1)
      | 'A'..'Z' ->
          if need_underscore then Buffer.add_char b '_';
          Buffer.add_char b c ;
          iter false (i+1)
      | _ ->
          Buffer.add_char b c ;
          iter true (i+1)
  in
  iter false 0;
  Buffer.contents b

let menhir_modules = StringSet.of_list [ "Incremental" ; "Recovery" ]

let check_name name =
  let upname = String.uppercase_ascii name in
  upname <> name &&
  not (OCAML_LANG.is_menhir_generated_file()
       &&
       StringSet.mem name menhir_modules)

let register ns
    ?(name="all_upper_struct")
    ~tags
    ?(msg = lint_msg)
    id
  =
  let w =
    YALO.new_warning ns ~name id
      ~tags
      ~msg
  in
  let warn ~file ~linter ~loc name =
    YALO.warn ~loc ~file ~linter w
      ~msg:(Printf.sprintf
              "Inner module %S should be fully uppercase \
               (%S here)" name
              (uppercase name))
  in
  if OCAML_LANG.is_running_as_ppx () then
    OCAML_LANG.new_ast_impl_traverse_linter ns
      ("check:typed:" ^ YALO_WARNING.name w)
      ~warnings:[ w ]
      OCAML_AST.(fun ~file ~linter traverse ->
          let mt_is_struct _m = true in
          let rec me_is_struct mod_expr =
            match mod_expr.pmod_desc with
            | Pmod_structure _
            | Pmod_unpack _ -> true
            | Pmod_constraint (mod_expr, _) -> me_is_struct mod_expr
            | _ -> false
          in
          let check mb_name is_struct mod_expr =
            match mb_name.txt with
            | None -> ()
            | Some name ->
                if check_name name && is_struct mod_expr then
                  warn ~file ~linter ~loc:mb_name.loc name
          in
          let expression ~file:_ ~linter:_ e =
            match e.pexp_desc with
            | Pexp_letmodule (mb_name, mb_expr, _) ->
                check mb_name me_is_struct mb_expr
            | _ -> ()
          in
          let module_binding ~file:_ ~linter:_ mb =
            check mb.pmb_name me_is_struct mb.pmb_expr
          in
          let module_expr ~file:_ ~linter:_ me =
            match me.pmod_desc with
            | Pmod_functor (Named (mb_name, mb_type), _) ->
                check mb_name mt_is_struct mb_type
            | _ -> ()
          in
          let module_type ~file:_ ~linter:_ mt = match mt.pmty_desc with
            | Pmty_functor (Named (mb_name, mb_type), _) ->
                check mb_name mt_is_struct mb_type
            | _ -> ()
          in
          traverse.module_binding <- (linter, module_binding) ::
                                     traverse.module_binding ;
          traverse.module_expr <- (linter, module_expr) ::
                                  traverse.module_expr ;
          traverse.module_type <- (linter, module_type) ::
                                  traverse.module_type ;
          traverse.expression <- (linter, expression) ::
                                 traverse.expression ;
        )
  else
    OCAML_LANG.new_tast_impl_traverse_linter ns
      ("check:typed:" ^ YALO_WARNING.name w)
      ~warnings:[ w ]
      OCAML_TAST.(fun ~file:_ ~linter traverse ->

          let check ~file ~linter mb_name mty =
            match (OCAML_TAST.module_binding_name mb_name).Location.txt with
            | None -> ()
            | Some name ->
                if check_name name then
                  match mty with
                  | Types.Mty_signature _ ->
                      warn ~file ~linter ~loc:mb_name.loc name
                  | Mty_ident _id -> ()
                  | Mty_functor _ -> ()

                  (* Aliases should follow this rule too, but only for user
                     defined aliases *)
                  | Mty_alias _ -> ()
          in

          let module_binding ~file ~linter mb =
            check ~file ~linter mb.mb_name mb.mb_expr.mod_type
          in
          let expression ~file ~linter exp =
            match exp.exp_desc with
            | Texp_letmodule (_mb_id, mb_name, _mb_presence, mb_expr, _exp) ->
                check ~file ~linter mb_name mb_expr.mod_type
            | _ -> ()
          in
          traverse.module_binding <- (linter, module_binding) ::
                                     traverse.module_binding ;
          traverse.expression <- (linter, expression) ::
                                 traverse.expression ;
          traverse.module_expr <- (linter, module_expr check) ::
                                  traverse.module_expr ;
          traverse.module_type <- (linter, module_type check) ::
                                  traverse.module_type ;
        )
